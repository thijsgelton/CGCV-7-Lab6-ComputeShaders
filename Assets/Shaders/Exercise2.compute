// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture

// declare the variables
RWTexture2D<float4> _MandelbrotSet;
Texture2D<float4> _ColorGradient;
SamplerState sampler_ColorGradient;

float _Left, _Right, _Bottom, _Top;
int _Iterations, _Bailout, _Width, _Height;

float EscapeTime(float2 c)
{
    // initialize z
    float2 z = 0;
    float escapeTime;

    // iterate z
    for(int i = 0; i < _Iterations; i++ )
    {
        z = float2(z.x * z.x - z.y * z.y + c.x, 2 * z.x * z.y + c.y);
        escapeTime = i / float(_Iterations);
        if (length(z) > _Bailout)
        {
            break;
        }
    }

    // return the escape time
    return escapeTime;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    // convert indices to coordinates
    float2 c;
    c.x = (_Right - _Left) * (id.x / float(_Width)) + _Left;
    c.y = (_Bottom - _Top) * (id.y / float(_Height)) + _Top;    
    
    // compute the escape time
    float escapeTime = EscapeTime(c);

    // sample the color
    float4 color = _ColorGradient.SampleLevel(sampler_ColorGradient, float2(escapeTime, 0), 0);
    
    // set the pixel value
    _MandelbrotSet[id.xy] = color;
}